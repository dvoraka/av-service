plugins {
    id "org.sonarqube" version "2.2"
}

apply plugin: 'distribution'

description = 'Project for anti-virus service'

distributions {
    main {
    }

//    checker {
//        baseName = 'checker'
//        contents {
//            from('checker/build/libs/') {
//                into 'lib/'
//            }
//            from('checker/LICENSE') {
//                into ''
//            }
//            from('checker/bin') {
//                into 'bin/'
//            }
//            from('checker/src/main/resources/log4j2.xml') {
//                into 'conf/'
//            }
//        }
//    }
}

allprojects {
    ext {
        taskGroupName = 'AV-service'
        // Spring versions
        springVersion = '4.3.4.RELEASE'
        springSecurityVersion = '4.1.3.RELEASE'
        springRabbitVersion = '1.6.5.RELEASE'
        springDataJpaVersion = '1.10.5.RELEASE'
        // Hibernate version
        hibernateVersion = '5.2.4.Final'
        // Ehcache version
        ehcacheVersion = '3.1.3'
        // ActiveMQ
        activemqVersion = '5.14.1'
        // Spock versions
        groovyVersion = '2.4.7'
        spockVersion = '1.1-groovy-2.4-rc-3'
        // Log4j version
        log4jVersion = '2.7'
        // Logback classic version
        logbackVersion = '1.1.6'
        // Logstash Logback encoder version
        logstashLogbackVersion = '4.7'
        // Cglib nodep version
        cglibVersion = '3.2.4'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'checkstyle'
    apply plugin: 'findbugs'
    apply plugin: 'pmd'
    apply plugin: 'jacoco'

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    version = '0.5-SNAPSHOT'

    repositories {
        mavenCentral()

//        // Spring snapshots
//        maven {
//            url "http://repo.spring.io/snapshot"
//        }
//        // Spring milestones
//        maven {
//            url "http://repo.spring.io/milestone"
//        }
//        // Spring libs milestones
//        maven {
//            url "http://repo.spring.io/libs-milestone"
//        }
    }

    sourceSets {
        // integration tests
        integrationTest {
            groovy {
                compileClasspath += main.output + test.output
                runtimeClasspath += main.output + test.output
                srcDir file('src/integration-test/groovy')
            }
            resources.srcDir file('src/integration-test/resources')
        }
        // performance tests
        performanceTest {
            groovy {
                compileClasspath += main.output + test.output
                runtimeClasspath += main.output + test.output
                srcDir file('src/performance-test/groovy')
            }
            resources.srcDir file('src/performance-test/resources')
        }
    }

    configurations {
        // IT
        integrationTestCompile.extendsFrom testCompile
        integrationTestRuntime.extendsFrom testRuntime
        // PT
        performanceTestCompile.extendsFrom testCompile
        performanceTestRuntime.extendsFrom testRuntime
    }

    dependencies {
        // Log4j
        compile("org.apache.logging.log4j:log4j-api:${log4jVersion}")
        // Log4j to SLF4J
        compile("org.apache.logging.log4j:log4j-to-slf4j:${log4jVersion}")
        // JCL to SLF4J
        compile("org.slf4j:jcl-over-slf4j:1.7.21")
        // Logback
        compile("ch.qos.logback:logback-classic:${logbackVersion}")
        // Logstash encoder
        compile("net.logstash.logback:logstash-logback-encoder:${logstashLogbackVersion}")
        // Spring
        compile("org.springframework:spring-core:${springVersion}")
        compile("org.springframework:spring-context:${springVersion}")
        // AMQP
        compile("org.springframework.amqp:spring-rabbit:${springRabbitVersion}") {
            exclude group: 'org.springframework', module: 'spring-web'
            exclude group: 'org.springframework', module: 'spring-tx'
            exclude group: 'org.springframework', module: 'spring-messaging'
        }

        // Groovy
        testCompile("org.codehaus.groovy:groovy-all:${groovyVersion}")
        // Spring test
        testCompile("org.springframework:spring-test:${springVersion}")
        // Spock
        testCompile("org.spockframework:spock-core:${spockVersion}")
        testCompile("org.spockframework:spock-spring:${spockVersion}")
        // Cglib
        testCompile("cglib:cglib-nodep:${cglibVersion}")
        testCompile("org.objenesis:objenesis:2.4")
        // EqualsVerifier
        testCompile("nl.jqno.equalsverifier:equalsverifier:2.1.7")
    }

    jar {
        baseName = "avservice-${project.name}"
    }

    test {
        maxParallelForks = 1

        minHeapSize = '128m'
        maxHeapSize = '128m'

        testLogging {
            exceptionFormat = 'full'
        }
    }

    task integrationTest(type: Test) {
        testClassesDir = sourceSets.integrationTest.output.classesDir
        classpath = sourceSets.integrationTest.runtimeClasspath

        mustRunAfter test

        minHeapSize = '128m'
        maxHeapSize = '128m'

        testLogging {
            // show events - "PASSED", "STARTED", "FAILED", "SKIPPED"
//            events "PASSED", "FAILED"
        }
    }

    task performanceTest(type: Test) {
        testClassesDir = sourceSets.performanceTest.output.classesDir
        classpath = sourceSets.performanceTest.runtimeClasspath
    }

    checkstyle {
        toolVersion = "7.1"
        configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
        reportsDir = file("${project.rootDir}/reports/checkstyle/${project.name}")
        sourceSets = [sourceSets.main]

        ignoreFailures = false
    }

    findbugs {
        reportsDir = file("${project.rootDir}/reports/findbugsReports/${project.name}")
        reportLevel = 'medium'
        sourceSets = [sourceSets.main]

        ignoreFailures = false
    }

    tasks.withType(FindBugs) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    pmd {
        toolVersion = '5.5.1'
        reportsDir = file("${project.rootDir}/reports/pmd/${project.name}")
        sourceSets = [sourceSets.main]

        ignoreFailures = false
    }

    jacoco {
        toolVersion = '0.7.7.201606060606'
        reportsDir = file("${project.rootDir}/reports/jacoco/${project.name}")
    }

    jacocoTestReport {
        executionData = fileTree(dir: 'build/jacoco', include: '**/*.exec')
        reports {
            xml.enabled = true
            html.enabled = true
        }
    }

    sonarqube {
        properties {
            property 'sonar.jacoco.reportPath', "${buildDir}/jacoco/test.exec"
            property 'sonar.jacoco.itReportPath', "${buildDir}/jacoco/integrationTest.exec"
        }
    }

    // set reporting HTML output directory
    tasks.withType(Test) {
        reports.html.destination = file("${project.rootDir}/reports/test/${project.name}")
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:all"
    }

    clean {
        delete "${project.rootDir}/reports/"
    }

    task copyToLib(type: Copy) {
        from(configurations.runtime) {
        }
        into "$buildDir/libs"
    }

    build.dependsOn copyToLib

    check.dependsOn integrationTest
    check.dependsOn jacocoTestReport
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.2.1'
}
