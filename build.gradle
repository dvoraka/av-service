plugins {
    id "org.akhikhl.gretty" version "1.2.4"
}

apply plugin: "org.akhikhl.gretty"

allprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
//    apply plugin: 'checkstyle'

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    version = '0.1-SNAPSHOT'
}

subprojects {
}

ext {
    springVersion = '4.2.4.RELEASE'
    spockVersion = '1.0-groovy-2.4'
}

repositories {
    mavenCentral()
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        groovy {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/groovy')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

dependencies {
    compile('org.apache.logging.log4j:log4j-api:2.5')
    compile('org.apache.logging.log4j:log4j-core:2.5')
    compile("org.springframework:spring-core:${springVersion}")
    compile("org.springframework:spring-context:${springVersion}")
    compile("org.springframework:spring-webmvc:${springVersion}")
    compile("org.springframework:spring-aspects:${springVersion}")
    compile('org.springframework.amqp:spring-rabbit:1.5.3.RELEASE')

    testCompile('junit:junit:4.12')
    testCompile("org.springframework:spring-test:${springVersion}")
    testCompile("org.spockframework:spock-spring:${spockVersion}")
    testCompile("org.spockframework:spock-core:${spockVersion}")
}

//checkstyle {
//    toolVersion = "6.5"
//}

test {
    testLogging {
        exceptionFormat = 'full'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.12-rc-1'
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath

    testLogging {
        // show events - "PASSED", "STARTED", "FAILED", "SKIPPED"
        events "PASSED", "FAILED", "SKIPPED"
    }
}

task runServer(type: JavaExec) {
    main = 'dvoraka.avservice.server.AmqpAVServer'
    classpath = sourceSets.main.runtimeClasspath
    environment('spring.profiles.active', 'amqp')
}

gretty {
    jvmArgs = ['-Dspring.profiles.active=rest,amqp']
    integrationTestTask = "none"
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test
