buildscript {
    ext {
        springBootVersion = '2.0.0.M7'
    }
    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    // SonarQube
    id 'org.sonarqube' version '2.5'
    // Spring Boot and dependency management
//    id 'org.springframework.boot' version '1.5.8.RELEASE'
    // SpotBugs
    id 'com.github.spotbugs' version '1.6.0'
}

description = 'Anti-virus and file network service'

allprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'checkstyle'
    apply plugin: 'com.github.spotbugs'
    apply plugin: 'pmd'
    apply plugin: 'jacoco'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'idea'
    apply plugin: 'distribution'

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    version = '0.10-SNAPSHOT'

    ext {
        taskGroupName = 'AV-service'
        // Log4j version
        log4jVersion = '2.9.1'
        // Logstash Logback encoder version
        logstashLogbackVersion = '4.11'
        // Spock version
        spockVersion = '1.1-groovy-2.4'
        // Cglib nodep version
        cglibVersion = '3.2.5'
        // Equals verifier version
        equalsVerifierVersion = '2.3.3'
    }

    repositories {
        mavenCentral()

        // Spring snapshots
        maven {
            url "http://repo.spring.io/snapshot"
        }
        // Spring milestones
        maven {
            url "http://repo.spring.io/milestone"
        }
        // Spring libs milestones
        maven {
            url "http://repo.spring.io/libs-milestone"
        }
    }

    sourceSets {
        // integration tests
        integrationTest {
            groovy {
                compileClasspath += main.output + test.output
                runtimeClasspath += main.output + test.output
                srcDir file('src/integration-test/groovy')
            }
            resources.srcDir file('src/integration-test/resources')
        }
        // performance tests
        performanceTest {
            groovy {
                compileClasspath += main.output + test.output
                runtimeClasspath += main.output + test.output
                srcDir file('src/performance-test/groovy')
            }
            resources.srcDir file('src/performance-test/resources')
        }
    }

    configurations {
        // IT
        integrationTestCompile.extendsFrom testCompile
        integrationTestRuntime.extendsFrom testRuntime
        // PT
        performanceTestCompile.extendsFrom testCompile
        performanceTestRuntime.extendsFrom testRuntime
    }

    idea {
        module {
            // add integration test source dirs to IDEA
            testSourceDirs += file('src/integration-test/groovy')
            scopes.TEST.plus += [configurations.integrationTestCompile]
        }
    }

    dependencyManagement {
        imports {
            mavenBom('org.springframework.boot:spring-boot-starter-parent:2.0.0.M7')
        }
    }

    dependencies {
        //
        // Logging
        //
        // Log4j API
        compile('org.apache.logging.log4j:log4j-api')
        // Log4j to SLF4J
        compile("org.apache.logging.log4j:log4j-to-slf4j:${log4jVersion}")
        // JCL to SLF4J & Logback
        compile('org.springframework.boot:spring-boot-starter-logging')
        // Logstash encoder
        compile("net.logstash.logback:logstash-logback-encoder:${logstashLogbackVersion}")

        //
        // Spring
        //
        // AMQP
        compile('org.springframework.boot:spring-boot-starter-amqp')
        // JMS
        compile('org.springframework.boot:spring-boot-starter-activemq')
        // Kafka
        compile('org.springframework.kafka:spring-kafka')
        // Data JPA & Hibernate
        compile('org.springframework.boot:spring-boot-starter-data-jpa')
        // Data Solr
        compile('org.springframework.boot:spring-boot-starter-data-solr')
        // PostgreSQL
        compile('org.postgresql:postgresql')

        // MVC & Security
        // Moved to REST module

        // Joda time (for Spring Solr)
        compile group: 'joda-time', name: 'joda-time'

        // Ehcache
        compile("org.ehcache:ehcache")

        // EL
        compile group: 'javax.el', name: 'javax.el-api'
        compile group: 'org.glassfish.web', name: 'javax.el', version: '2.2.6'

        // H2 DB
        compile group: 'com.h2database', name: 'h2'

        //
        // Testing
        //
        testCompile('org.springframework.boot:spring-boot-starter-test') {
            exclude module: 'assertj-core'
            exclude module: 'jsonassert'
            exclude module: 'json-path'
        }
        // Spock
        testCompile("org.spockframework:spock-core:${spockVersion}")
        testCompile("org.spockframework:spock-spring:${spockVersion}")
        // Cglib
        testCompile("cglib:cglib-nodep:${cglibVersion}")
        // Objenesis
        testCompile('org.objenesis:objenesis')
        // Equals verifier
        testCompile("nl.jqno.equalsverifier:equalsverifier:${equalsVerifierVersion}")
    }

    jar {
        baseName = "avservice-${project.name}"
    }

    test {
        maxParallelForks = 2

        minHeapSize = '256m'
        maxHeapSize = '256m'

        testLogging {
            exceptionFormat = 'full'
        }
    }

    task integrationTest(type: Test) {
        testClassesDirs = sourceSets.integrationTest.output.classesDirs
        classpath = sourceSets.integrationTest.runtimeClasspath

        mustRunAfter test

        systemProperty('itest', 'true')

        minHeapSize = '512m'
        maxHeapSize = '512m'

        testLogging {
            // show events - "PASSED", "STARTED", "FAILED", "SKIPPED"
//            events "PASSED", "FAILED"
            exceptionFormat = 'full'
        }
    }

    task performanceTest(type: Test) {
        testClassesDirs = sourceSets.performanceTest.output.classesDirs
        classpath = sourceSets.performanceTest.runtimeClasspath
    }

    //
    // Quality tool configurations
    //
    checkstyle {
        toolVersion = '8.0'
        configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
        reportsDir = file("${project.rootDir}/reports/checkstyle/${project.name}")
        sourceSets = [sourceSets.main]

        ignoreFailures = true
    }

    spotbugs {
        reportsDir = file("${project.rootDir}/reports/spotbugsReports/${project.name}")
        sourceSets = [sourceSets.main]

        effort = 'max'
        reportLevel = 'high'

        ignoreFailures = false
    }

    tasks.withType(com.github.spotbugs.SpotBugsTask) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    pmd {
        reportsDir = file("${project.rootDir}/reports/pmd/${project.name}")
        sourceSets = [sourceSets.main]

        ignoreFailures = false
    }

    jacoco {
        reportsDir = file("${project.rootDir}/reports/jacoco/${project.name}")
    }

    jacocoTestReport {
        executionData = fileTree(dir: 'build/jacoco', include: '**/*.exec')
        reports {
            xml.enabled = true
            html.enabled = true
        }
    }

    sonarqube {
        properties {
            property 'sonar.jacoco.reportPath', "${buildDir}/jacoco/test.exec"
            property 'sonar.jacoco.itReportPath', "${buildDir}/jacoco/integrationTest.exec"
        }
    }

    // set reporting HTML output directory
    tasks.withType(Test) {
        reports.html.destination = file("${project.rootDir}/reports/test/${project.name}")
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:all"
    }

    clean {
        delete "${project.rootDir}/reports/"
    }

    task copyToLib(type: Copy) {
        from(configurations.runtime) {
        }
        into "$buildDir/libs"
    }

    build.dependsOn copyToLib

    check.dependsOn integrationTest
    check.dependsOn jacocoTestReport
    check.dependsOn javadoc
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.4.1'
}
