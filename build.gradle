plugins {
    // SonarQube
    id 'org.sonarqube' version '2.2.1'
    // Spring Boot and dependency management
    id 'org.springframework.boot' version '1.4.3.RELEASE'
}

apply plugin: 'distribution'

description = 'Project for anti-virus service'

distributions {
    main {
    }

//    checker {
//        baseName = 'checker'
//        contents {
//            from('checker/build/libs/') {
//                into 'lib/'
//            }
//            from('checker/LICENSE') {
//                into ''
//            }
//            from('checker/bin') {
//                into 'bin/'
//            }
//            from('checker/src/main/resources/log4j2.xml') {
//                into 'conf/'
//            }
//        }
//    }
}

allprojects {
    ext {
        taskGroupName = 'AV-service'

        // Log4j version
        log4jVersion = '2.7'
        // Logstash Logback encoder version
        logstashLogbackVersion = '4.7'
        // Cglib nodep version
        cglibVersion = '3.2.4'
        // Equals verifier version
        equalsVerifierVersion = '2.1.7'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'checkstyle'
    apply plugin: 'findbugs'
    apply plugin: 'pmd'
    apply plugin: 'jacoco'
    apply plugin: 'org.springframework.boot'

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    version = '0.5-SNAPSHOT'

    repositories {
        mavenCentral()

        // Spring snapshots
        maven {
            url "http://repo.spring.io/snapshot"
        }
        // Spring milestones
        maven {
            url "http://repo.spring.io/milestone"
        }
        // Spring libs milestones
        maven {
            url "http://repo.spring.io/libs-milestone"
        }
    }

    sourceSets {
        // integration tests
        integrationTest {
            groovy {
                compileClasspath += main.output + test.output
                runtimeClasspath += main.output + test.output
                srcDir file('src/integration-test/groovy')
            }
            resources.srcDir file('src/integration-test/resources')
        }
        // performance tests
        performanceTest {
            groovy {
                compileClasspath += main.output + test.output
                runtimeClasspath += main.output + test.output
                srcDir file('src/performance-test/groovy')
            }
            resources.srcDir file('src/performance-test/resources')
        }
    }

    configurations {
        // IT
        integrationTestCompile.extendsFrom testCompile
        integrationTestRuntime.extendsFrom testRuntime
        // PT
        performanceTestCompile.extendsFrom testCompile
        performanceTestRuntime.extendsFrom testRuntime
    }

    dependencyManagement {
        imports {
            mavenBom('io.spring.platform:platform-bom:Athens-SR1') {
                // temporary BOM override for Spring 5
                bomProperty 'spring.version', '5.0.0.M4'
                bomProperty 'spring-amqp.version', '2.0.0.M1'
                bomProperty 'spring-data-releasetrain.version', 'Kay-M1'

                // temporary override for compatibility
                bomProperty 'guava.version', '18.0'
                bomProperty 'logback.version', '1.1.6'
                bomProperty 'log4j2.version', '2.7'
                bomProperty 'spock.version', '1.1-groovy-2.4-rc-3'
            }
        }
    }

    dependencies {
        //
        // Logging
        //
        // Log4j
        compile('org.apache.logging.log4j:log4j-api')
        // Log4j to SLF4J
        compile("org.apache.logging.log4j:log4j-to-slf4j:${log4jVersion}")
        // JCL to SLF4J
        compile('org.slf4j:jcl-over-slf4j')
        // Logback
        compile('ch.qos.logback:logback-classic')
        // Logstash encoder
        compile("net.logstash.logback:logstash-logback-encoder:${logstashLogbackVersion}")

        //
        // Spring
        //
        // core
        compile('org.springframework:spring-core')
        compile('org.springframework:spring-context')
        // AMQP
        compile 'org.springframework.amqp:spring-rabbit'
        // Aspects
        compile('org.springframework:spring-aspects')
        // JMS
        compile('org.springframework:spring-jms')
        compile('org.apache.activemq:activemq-broker')
        // Data
        compile('org.springframework:spring-orm')
        compile('org.springframework.data:spring-data-jpa')
        compile('org.springframework.data:spring-data-solr')
        // Hibernate
        compile('org.hibernate:hibernate-entitymanager')
        // PostgreSQL
        compile('org.postgresql:postgresql')
        // MVC
        compile('org.springframework:spring-webmvc')
        // Security
        compile('org.springframework.security:spring-security-config')
        compile('org.springframework.security:spring-security-web')

        // Spring 5 deps
        compile group: 'javax.jms', name: 'javax.jms-api', version: '2.0.1'

        // Ehcache
        compile("org.ehcache:ehcache")

        // Hibernate validator
        compile('org.hibernate:hibernate-validator')

        // EL
        compile group: 'javax.el', name: 'javax.el-api'
        compile group: 'org.glassfish.web', name: 'javax.el', version: '2.2.6'

        //
        // Testing
        //
        // Groovy
        testCompile('org.codehaus.groovy:groovy-all')
        // Spring test
        testCompile('org.springframework:spring-test')
        // Spock
        testCompile('org.spockframework:spock-core')
        testCompile('org.spockframework:spock-spring')
        // Cglib
        testCompile("cglib:cglib-nodep:${cglibVersion}")
        // Objenesis
        testCompile('org.objenesis:objenesis')
        // Equals verifier
        testCompile("nl.jqno.equalsverifier:equalsverifier:${equalsVerifierVersion}")
    }

    jar {
        baseName = "avservice-${project.name}"
    }

    test {
        maxParallelForks = 1

        minHeapSize = '128m'
        maxHeapSize = '128m'

        testLogging {
            exceptionFormat = 'full'
        }
    }

    task integrationTest(type: Test) {
        testClassesDir = sourceSets.integrationTest.output.classesDir
        classpath = sourceSets.integrationTest.runtimeClasspath

        mustRunAfter test

        minHeapSize = '128m'
        maxHeapSize = '128m'

        testLogging {
            // show events - "PASSED", "STARTED", "FAILED", "SKIPPED"
//            events "PASSED", "FAILED"
        }
    }

    task performanceTest(type: Test) {
        testClassesDir = sourceSets.performanceTest.output.classesDir
        classpath = sourceSets.performanceTest.runtimeClasspath
    }

    //
    // Quality tools configuration
    //
    checkstyle {
        toolVersion = '7.3'
        configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
        reportsDir = file("${project.rootDir}/reports/checkstyle/${project.name}")
        sourceSets = [sourceSets.main]

        ignoreFailures = false
    }

    findbugs {
        toolVersion = '3.0.1'
        reportsDir = file("${project.rootDir}/reports/findbugsReports/${project.name}")
        reportLevel = 'medium'
        sourceSets = [sourceSets.main]

        ignoreFailures = false
    }

    tasks.withType(FindBugs) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    pmd {
        toolVersion = '5.5.2'
        reportsDir = file("${project.rootDir}/reports/pmd/${project.name}")
        sourceSets = [sourceSets.main]

        ignoreFailures = false
    }

    jacoco {
        toolVersion = '0.7.8'
        reportsDir = file("${project.rootDir}/reports/jacoco/${project.name}")
    }

    jacocoTestReport {
        executionData = fileTree(dir: 'build/jacoco', include: '**/*.exec')
        reports {
            xml.enabled = true
            html.enabled = true
        }
    }

    sonarqube {
        properties {
            property 'sonar.jacoco.reportPath', "${buildDir}/jacoco/test.exec"
            property 'sonar.jacoco.itReportPath', "${buildDir}/jacoco/integrationTest.exec"
        }
    }

    // set reporting HTML output directory
    tasks.withType(Test) {
        reports.html.destination = file("${project.rootDir}/reports/test/${project.name}")
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:all"
    }

    clean {
        delete "${project.rootDir}/reports/"
    }

    task copyToLib(type: Copy) {
        from(configurations.runtime) {
        }
        into "$buildDir/libs"
    }

    build.dependsOn copyToLib

    check.dependsOn integrationTest
    check.dependsOn jacocoTestReport
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.2.1'
}
